plugins {
    id 'com.android.application'
}

def jsEngine = "V8"
if (project.hasProperty("jsEngine")) {
    jsEngine = project.property("jsEngine")
}

def targetApiLevel = 35
def requiredNdkVersion = project.findProperty("ndkVersion") ?: "28.2.13676358"

android {
    namespace 'com.jsruntimehost.unittests'
    compileSdk targetApiLevel
    ndkVersion = requiredNdkVersion

    defaultConfig {
        applicationId "com.jsruntimehost.unittests"
        minSdk 21
        targetSdk targetApiLevel
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                arguments(
                    "-DANDROID_STL=c++_shared",
                    "-DNAPI_JAVASCRIPT_ENGINE=${jsEngine}",
                    "-DJSRUNTIMEHOST_CORE_APPRUNTIME_V8_INSPECTOR=ON",
                    "-DJSRUNTIMEHOST_ANDROID_TARGET_API=${targetApiLevel}"
                )
            }
        }

        if (project.hasProperty("abiFilters")) {
            ndk {
                abiFilters project.getProperty("abiFilters")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            buildStagingDirectory '../../../../Build/Android'
            version '3.22.1+'
        }
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.0'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation group: 'org.java-websocket', name: 'Java-WebSocket', version: '1.5.3'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

task copyScripts {
    doLast {
        // run copy at execution phase because npm command is not done at configuration phase
        copy
        {
            from '../../dist'
            include "*.js"
            into 'src/main/assets/Scripts'
        }
        copy
        {
            from '../../Scripts'
            include "*.js"
            into 'src/main/assets/Scripts'
        }
    }
}

// Run copyScripts task after CMake external build
// And make sure merging assets into output is performed after the scripts copy
tasks.configureEach { task ->
    if (task.name == 'mergeDebugNativeLibs') {
        task.finalizedBy(copyScripts)
    }
    if (task.name == 'mergeDebugAssets') {
        task.dependsOn(copyScripts)
    }
}

afterEvaluate {
    def androidExtension = project.extensions.findByName('android')
    def sdkDir = androidExtension?.sdkDirectory
    def ndkDir = androidExtension?.ndkDirectory

    if (sdkDir != null) {
        def sdkPath = sdkDir.absolutePath
        tasks.withType(com.android.build.gradle.tasks.ExternalNativeBuildTask).configureEach { task ->
            task.environment("ANDROID_SDK_ROOT", sdkPath)
            task.environment("ANDROID_HOME", sdkPath)
        }
        tasks.withType(com.android.build.gradle.tasks.ExternalNativeBuildJsonTask).configureEach { task ->
            task.environment("ANDROID_SDK_ROOT", sdkPath)
            task.environment("ANDROID_HOME", sdkPath)
        }
    }

    if (ndkDir == null && sdkDir != null) {
        def candidate = new File(sdkDir, "ndk/${requiredNdkVersion}")
        if (candidate.exists()) {
            ndkDir = candidate
        }
    }

    if (ndkDir != null) {
        def ndkPath = ndkDir.absolutePath
        if (androidExtension?.hasProperty('ndkPath')) {
            androidExtension.ndkPath = ndkPath
        }
        tasks.withType(com.android.build.gradle.tasks.ExternalNativeBuildTask).configureEach { task ->
            task.environment("ANDROID_NDK", ndkPath)
            task.environment("ANDROID_NDK_HOME", ndkPath)
            task.environment("ANDROID_NDK_ROOT", ndkPath)
        }
        tasks.withType(com.android.build.gradle.tasks.ExternalNativeBuildJsonTask).configureEach { task ->
            task.environment("ANDROID_NDK", ndkPath)
            task.environment("ANDROID_NDK_HOME", ndkPath)
            task.environment("ANDROID_NDK_ROOT", ndkPath)
        }
    } else if (sdkDir != null) {
        logger.lifecycle("[JSRUNTIMEHOST_ANDROID] Expected Android NDK ${requiredNdkVersion} under ${sdkDir}. Install it with 'sdkmanager \"ndk;${requiredNdkVersion}\"'.")
    }
}
