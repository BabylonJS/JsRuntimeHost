plugins {
    id 'com.android.application'
}

def jsEngine = "V8"
if (project.hasProperty("jsEngine")) {
    jsEngine = project.property("jsEngine")
}

def targetApiLevel = 35
def requiredNdkVersion = project.findProperty("ndkVersion") ?: "28.2.13676358"

android {
    namespace 'com.jsruntimehost.unittests'
    compileSdk targetApiLevel
    ndkVersion = requiredNdkVersion

    defaultConfig {
        applicationId "com.jsruntimehost.unittests"
        minSdk 21
        targetSdk targetApiLevel
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                arguments(
                    "-DANDROID_STL=c++_shared",
                    "-DNAPI_JAVASCRIPT_ENGINE=${jsEngine}",
                    "-DJSRUNTIMEHOST_CORE_APPRUNTIME_V8_INSPECTOR=ON"
                )
            }
        }

        ndk {
            def abiFiltersProp = project.findProperty("abiFilters")?.toString()
            if (abiFiltersProp) {
                def propFilters = abiFiltersProp.split(',').collect { it.trim() }.findAll { !it.isEmpty() }
                if (!propFilters.isEmpty()) {
                    abiFilters(*propFilters)
                }
            } else {
                // Prefer injected ABI hints and fall back to a host-aware default
                def requestedAbi = project.findProperty("android.injected.build.abi") ?: System.getenv("ANDROID_ABI")
                def defaultAbis = []
                if (requestedAbi) {
                    defaultAbis = requestedAbi.split(',').collect { it.trim() }.findAll { !it.isEmpty() }
                }
                if (defaultAbis.isEmpty()) {
                    def hostArch = (System.getProperty("os.arch") ?: "").toLowerCase()
                    if (hostArch.contains("aarch64") || hostArch.contains("arm64")) {
                        defaultAbis = ['arm64-v8a']
                    } else {
                        defaultAbis = ['arm64-v8a', 'x86_64']
                    }
                }
                abiFilters(*defaultAbis)
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            buildStagingDirectory '../../../../Build/Android'
            version '3.22.1+'
        }
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.0'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation group: 'org.java-websocket', name: 'Java-WebSocket', version: '1.5.3'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

task copyScripts {
    doLast {
        // run copy at execution phase because npm command is not done at configuration phase
        copy
        {
            from '../../dist'
            include "*.js"
            into 'src/main/assets/Scripts'
        }
        copy
        {
            from '../../Scripts'
            include "*.js"
            into 'src/main/assets/Scripts'
        }
    }
}

// Run copyScripts task after CMake external build
// And make sure merging assets into output is performed after the scripts copy
tasks.configureEach { task ->
    if (task.name == 'mergeDebugNativeLibs') {
        task.finalizedBy(copyScripts)
    }
    if (task.name == 'mergeDebugAssets') {
        task.dependsOn(copyScripts)
    }
}
