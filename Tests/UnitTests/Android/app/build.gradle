plugins {
    id 'com.android.application'
}

def jsEngine = "V8"
if (project.hasProperty("jsEngine")) {
    jsEngine = project.property("jsEngine")
}

def nodeApiAssetsDir = "${project.buildDir}/generated/nodeapiassets"
def enableAsan = project.hasProperty("enableAsan")

android {
    namespace 'com.jsruntimehost.unittests'
    compileSdk 33
    ndkVersion = "23.1.7779620"
    if (project.hasProperty("ndkVersion")) {
        ndkVersion = project.property("ndkVersion")
    }

    defaultConfig {
        applicationId "com.jsruntimehost.unittests"
        minSdk 21
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                def cmakeArgs = [
                    "-DANDROID_STL=c++_shared",
                    "-DNAPI_JAVASCRIPT_ENGINE=${jsEngine}",
                    "-DJSRUNTIMEHOST_CORE_APPRUNTIME_V8_INSPECTOR=ON"
                ]
                if (enableAsan) {
                    cmakeArgs += "-DJSR_ENABLE_ASAN=ON"
                }
                arguments(*cmakeArgs)
            }
        }

        if (project.hasProperty("abiFilters")) {
            ndk {
                abiFilters project.getProperty("abiFilters")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            buildStagingDirectory '../../../../Build/Android'
            version '3.22.1+'
        }
    }
    buildFeatures {
        viewBinding true
    }

    packagingOptions {
        if (enableAsan) {
            doNotStrip "**/*.so"
            jniLibs.useLegacyPackaging true
        }
    }

    sourceSets {
        main {
            assets.srcDirs += [nodeApiAssetsDir]
            if (enableAsan) {
                jniLibs.srcDir "${buildDir}/generated/asanRuntime/jniLibs"
            }
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.0'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation group: 'org.java-websocket', name: 'Java-WebSocket', version: '1.5.3'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

task copyScripts {
    doLast {
        // run copy at execution phase because npm command is not done at configuration phase
        copy
        {
            from '../../dist'
            include "*.js"
            into 'src/main/assets/Scripts'
        }
        copy
        {
            from '../../Scripts'
            include "*.js"
            into 'src/main/assets/Scripts'
        }
    }
}

task copyNodeApiTests(type: Copy) {
    from '../../../NodeApi/test'
    into "${nodeApiAssetsDir}/NodeApi/test"
}

// Run copyScripts task after CMake external build
// And make sure merging assets into output is performed after the scripts copy
tasks.configureEach { task ->
    if (task.name == 'mergeDebugNativeLibs') {
        task.finalizedBy(copyScripts)
    }
    if (task.name == 'mergeDebugAssets') {
        task.dependsOn(copyScripts)
        task.dependsOn(copyNodeApiTests)
    }
    if (task.name == 'mergeReleaseAssets') {
        task.dependsOn(copyScripts)
        task.dependsOn(copyNodeApiTests)
    }
}

preBuild.dependsOn(copyNodeApiTests)

if (enableAsan) {
    def hostTag = {
        def osName = System.getProperty("os.name").toLowerCase()
        if (osName.contains("mac") || osName.contains("darwin")) {
            return "darwin-x86_64"
        } else if (osName.contains("windows")) {
            return "windows-x86_64"
        } else {
            return "linux-x86_64"
        }
    }.call()

    def asanRuntimeProvider = providers.provider {
        def prebuiltRoot = new File(android.ndkDirectory, "toolchains/llvm/prebuilt/${hostTag}/lib64/clang")
        if (!prebuiltRoot.exists()) {
            throw new GradleException("Unable to locate clang libraries under ${prebuiltRoot}")
        }
        def versionDir = prebuiltRoot.listFiles().find { it.isDirectory() }
        if (versionDir == null) {
            throw new GradleException("Unable to determine clang version directory within ${prebuiltRoot}")
        }
        def runtimeFile = new File(versionDir, "lib/linux/libclang_rt.asan-aarch64-android.so")
        if (!runtimeFile.exists()) {
            throw new GradleException("Unable to locate ASan runtime library at ${runtimeFile}")
        }
        return runtimeFile
    }

    def generatedAsanDir = layout.buildDirectory.dir("generated/asanRuntime/jniLibs/arm64-v8a")

    def prepareAsanRuntime = tasks.register("prepareAsanRuntime", Copy) {
        from(asanRuntimeProvider)
        into(generatedAsanDir)
    }

    tasks.matching { it.name in ["mergeDebugNativeLibs", "mergeReleaseNativeLibs"] }.configureEach {
        dependsOn(prepareAsanRuntime)
    }

    def wrapScript = file("${projectDir}/../tools/wrap.sh")

    def pushAsanRuntime = tasks.register("pushAsanRuntime", Exec) {
        dependsOn(prepareAsanRuntime)
        commandLine("adb", "push",
                generatedAsanDir.get().file("libclang_rt.asan-aarch64-android.so").asFile.absolutePath,
                "/data/local/tmp/libclang_rt.asan-aarch64-android.so")
    }

    def pushAsanWrapScript = tasks.register("pushAsanWrapScript", Exec) {
        commandLine("adb", "push", wrapScript.absolutePath, "/data/local/tmp/wrap.sh")
    }

    tasks.register("pushAsanArtifacts") {
        dependsOn(pushAsanRuntime, pushAsanWrapScript)
        doLast {
            exec {
                commandLine("adb", "shell", "chcon", "u:object_r:zygote_exec:s0", "/data/local/tmp/wrap.sh")
            }
            exec {
                commandLine("adb", "shell", "chmod", "+x", "/data/local/tmp/wrap.sh")
            }
        }
    }
}
