plugins {
    id 'com.android.application'
}

def jsEngine = "V8"
if (project.hasProperty("jsEngine")) {
    jsEngine = project.property("jsEngine")
}

def targetApiLevel = 35
def requiredNdkVersion = "28.2.13676358"
if (project.hasProperty("ndkVersion")) {
    requiredNdkVersion = project.property("ndkVersion")
}
def resolvePreferredNdkDir = {
    def androidExtension = project.extensions.findByName('android')
    if (androidExtension != null && androidExtension.hasProperty('ndkDirectory')) {
        def candidate = androidExtension.ndkDirectory
        if (candidate != null && candidate.exists()) {
            return candidate
        }
    }

    def sdkRoots = [] as LinkedHashSet
    def sdkRootEnv = System.getenv("ANDROID_SDK_ROOT")
    if (sdkRootEnv) {
        sdkRoots.add(sdkRootEnv)
    }
    def androidHomeEnv = System.getenv("ANDROID_HOME")
    if (androidHomeEnv) {
        sdkRoots.add(androidHomeEnv)
    }

    for (root in sdkRoots) {
        def candidate = new File(root, "ndk/${requiredNdkVersion}")
        if (candidate.exists()) {
            return candidate
        }
    }

    return null
}

android {
    namespace 'com.jsruntimehost.unittests'
    compileSdk targetApiLevel
    ndkVersion = requiredNdkVersion

    defaultConfig {
        applicationId "com.jsruntimehost.unittests"
        minSdk 21
        targetSdk targetApiLevel
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                def cmakeArguments = [
                    "-DANDROID_STL=c++_shared",
                    "-DNAPI_JAVASCRIPT_ENGINE=${jsEngine}",
                    "-DJSRUNTIMEHOST_CORE_APPRUNTIME_V8_INSPECTOR=ON",
                    "-DJSRUNTIMEHOST_ANDROID_TARGET_API=${targetApiLevel}"
                ]

                def resolvedNdkDir = resolvePreferredNdkDir()
                if (resolvedNdkDir != null) {
                    def ndkPath = resolvedNdkDir.absolutePath
                    cmakeArguments += [
                        "-DANDROID_NDK=${ndkPath}",
                        "-DCMAKE_ANDROID_NDK=${ndkPath}",
                        "-DCMAKE_TOOLCHAIN_FILE=${ndkPath}/build/cmake/android.toolchain.cmake"
                    ]
                }

                arguments(*cmakeArguments)
            }
        }

        if (project.hasProperty("abiFilters")) {
            ndk {
                abiFilters project.getProperty("abiFilters")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            buildStagingDirectory '../../../../Build/Android'
            version '3.22.1+'
        }
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.0'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation group: 'org.java-websocket', name: 'Java-WebSocket', version: '1.5.3'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

task copyScripts {
    doLast {
        // run copy at execution phase because npm command is not done at configuration phase
        copy
        {
            from '../../dist'
            include "*.js"
            into 'src/main/assets/Scripts'
        }
        copy
        {
            from '../../Scripts'
            include "*.js"
            into 'src/main/assets/Scripts'
        }
    }
}

// Run copyScripts task after CMake external build
// And make sure merging assets into output is performed after the scripts copy
tasks.configureEach { task ->
    if (task.name == 'mergeDebugNativeLibs') {
        task.finalizedBy(copyScripts)
    }
    if (task.name == 'mergeDebugAssets') {
        task.dependsOn(copyScripts)
    }
}

afterEvaluate {
    def preferredNdkDir = resolvePreferredNdkDir()
    if (preferredNdkDir != null) {
        def ndkPath = preferredNdkDir.absolutePath
        tasks.withType(com.android.build.gradle.tasks.ExternalNativeBuildJsonTask).configureEach { task ->
            task.environment("ANDROID_NDK", ndkPath)
            task.environment("ANDROID_NDK_HOME", ndkPath)
            task.environment("ANDROID_NDK_ROOT", ndkPath)
        }
        tasks.withType(com.android.build.gradle.tasks.ExternalNativeBuildTask).configureEach { task ->
            task.environment("ANDROID_NDK", ndkPath)
            task.environment("ANDROID_NDK_HOME", ndkPath)
            task.environment("ANDROID_NDK_ROOT", ndkPath)
        }
    }
}
